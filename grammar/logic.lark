// Propositional logic grammar with predicates
// Precedence (lowest to highest): <->, ->, |, &, ~

?start: bicond

?bicond: implies
       | bicond "<->" implies  -> logic_bicond

?implies: or_expr
        | or_expr "->" implies -> logic_implies   // right-associative

?or_expr: and_expr
        | or_expr "|" and_expr -> logic_or

?and_expr: unary
         | and_expr "&" unary  -> logic_and

?unary: "~" unary              -> logic_not
      | atom

?atom: predicate
     | CONST_TRUE              -> const_true
     | CONST_FALSE             -> const_false
     | "(" bicond ")"
     | variable

predicate: NAME "(" arguments ")"
arguments: argument ("," argument)*
argument: ESCAPED_STRING
        | NAME
        | NUMBER

variable: NAME

CONST_TRUE: "true"
CONST_FALSE: "false"

NAME: /(?!true\b|false\b)[a-zA-Z_][a-zA-Z0-9_]*/
NUMBER: /[0-9]+(\.[0-9]+)?/

%import common.ESCAPED_STRING
%import common.WS
%ignore WS
